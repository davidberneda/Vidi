Test {
  Numbers : Integer[] := [1,2,3,4]
  
  Foo() {
    Console.PutLine('Range loop')
    
    for x in 1..2  {
       Console.Put(x,' ')  // 1 2
    }
    
    Console.PutLine
    
    Console.PutLine('Array loop')
    
    for x in Numbers {
       Console.Put(x,' ')  // 1 2 3 4
    }

    Console.PutLine

    Console.PutLine('Number loop')

    for x::=5 to 7  {
       Console.Put(x,' ')  // 5 6 7
    }

    Console.PutLine

    Console.PutLine('Text loop')

    for x in 'ABC' {
       Console.Put(x,' ') // A B C
    }

    Console.PutLine

    Console.PutLine('Expression loop')

    Bar:Float[] { [3.14, 2.71] }
    
    for x in Bar {
       Console.Put(x,' ')  // 3.14 2.71
    }

    Console.PutLine

    Console.PutLine('Range function loop')

    with ast
    
    x::=2
    
    FooRange:Range { 20..22+x }
    
    for x in FooRange {
       Console.Put(x,' ')  // 20 21 22 23 24
    }

    Console.PutLine

    Console.PutLine('Class loop')

    Items is SomeThing[] {}
    
    Things: Items := [ 1, "A", True, 2.2 ]
    
    for x in Things {
       Console.Put(x,' ') // 1 A True 2.2
    }

    Console.PutLine

    Console.PutLine('Indexed loop')

    Items2 {
      indexed List:Text[]
    }
    
    Things2: Items2
    Things2.List := [ "X", 'Y', 'Z' ]
    
    for x in Things2 {
       Console.Put(x,' ') // X Y Z
    }

    Console.PutLine
  }
}

{
  T:Test
  T.Foo
}

//OUT: Range loop^
//OUT: 1 2 ^
//OUT: Array loop^
//OUT: 1 2 3 4 ^
//OUT: Number loop^
//OUT: 5 6 7 ^
//OUT: Text loop^
//OUT: A B C ^
//OUT: Expression loop^
//OUT: 3.14 2.71 ^
//OUT: Range function loop^
//OUT: 20 21 22 23 24 ^
//OUT: Class loop^
//OUT: 1 A True 2.2 ^
//OUT: Indexed loop^
//OUT: X Y Z ^

